apply plugin: 'com.android.application'
if (getGradle().getStartParameter().getTaskRequests().toString().contains("Release")) {
    apply plugin: 'com.google.gms.google-services'
    apply plugin: 'com.google.firebase.crashlytics'
}
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    buildFeatures {
        viewBinding true
    }
    buildToolsVersion buildTools
    buildTypes {
        debug {
            applicationIdSuffix '.dbg'
        }
        release {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            /*
            signingConfigs {
                store_upload_signing {
                    storeFile rootProject.file(keyRing["UPLOAD_KEYSTORE_FILE"])
                    storePassword keyRing["UPLOAD_KEYSTORE_PASSWORD"]
                    keyAlias keyRing["UPLOAD_KEYSTORE_ALIAS"]
                    keyPassword keyRing["UPLOAD_KEYSTORE_PASSWORD"]
                }
            }
            signingConfig signingConfigs.store_upload_signing
             */
        }
    }
    compileSdkVersion 30
    defaultConfig {
        applicationId "com.hzlgrn.pdxrail"
        buildConfigField "long", "BUILD_TIME", "${buildTime}L"
        buildConfigField "String", "KEY_API_RAIL_SYSTEM", "\""+keyRing["KEY_API_RAIL_SYSTEM"]+"\""
        buildConfigField "String", "URL_API_RAIL_SYSTEM", "\""+keyRing["URL_API_RAIL_SYSTEM"]+"\""
        buildConfigField "String", "HOME_URL", "\""+keyRing["HOME_URL"]+"\""
        buildConfigField "String", "HOME_HOST", "\""+keyRing["HOME_HOST"]+"\""
        manifestPlaceholders = [
            APPLICATION_LABEL: "PDX Rail",
            KEY_GEO_ANDROID_GOOGLE  : keyRing["KEY_API_GOOGLE"],
            HOME_HOST: keyRing["HOME_HOST"],
            HOME_URL: keyRing["HOME_URL"]
        ]
        minSdkVersion 23
        resValue "string", "asset_statements", assetStatements
        targetSdkVersion 30
        vectorDrawables.useSupportLibrary = true
        versionCode buildVersionCode
        versionName buildVersionName

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation":"$projectDir/schemas".toString(),
                        "room.incremental":"true",
                        "room.expandProjection":"true"]
            }
        }
    }
}
dependencies {
    implementation platform('com.google.firebase:firebase-bom:28.2.1')

    implementation "androidx.appcompat:appcompat:1.3.1"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.constraintlayout:constraintlayout:2.1.0"
    implementation "androidx.core:core-ktx:1.6.0"
    implementation "androidx.recyclerview:recyclerview:1.2.1"
    implementation "com.google.android.gms:play-services-instantapps:17.0.1"
    implementation "com.google.android.gms:play-services-location:18.0.0"
    implementation "com.google.android.gms:play-services-maps:17.0.1"
    implementation "com.google.android.material:material:1.4.0"
    implementation "com.google.firebase:firebase-analytics"
    implementation "com.google.firebase:firebase-crashlytics-ktx"
    implementation "com.google.firebase:firebase-analytics-ktx"
    implementation 'com.google.maps.android:android-maps-utils:2.2.5'
    implementation "com.jakewharton.timber:timber:4.7.1"
    implementation "com.squareup.okhttp3:okhttp:4.9.1"
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$version_kotlin"

    def version_coroutines = '1.5.1-native-mt'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$version_coroutines"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$version_coroutines"

    // Dagger
    def version_dagger = '2.37'
    implementation "com.google.dagger:dagger:$version_dagger"
    kapt "com.google.dagger:dagger-compiler:$version_dagger"

    // Lifecycle
    def version_lifecycle = '2.2.0'
    implementation "androidx.lifecycle:lifecycle-extensions:$version_lifecycle"

    // Moshi
    def version_moshi = '1.12.0'
    implementation "com.squareup.moshi:moshi:$version_moshi"
    implementation "com.squareup.moshi:moshi-kotlin:$version_moshi"
    implementation 'com.squareup.retrofit2:converter-moshi:2.9.0'

    // Room
    def version_room = '2.3.0'
    implementation "androidx.room:room-runtime:$version_room"
    implementation "androidx.room:room-ktx:$version_room"
    kapt "androidx.room:room-compiler:$version_room"

    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$version_coroutines"

    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}
