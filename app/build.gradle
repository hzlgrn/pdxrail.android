apply plugin: 'com.android.application'
if (getGradle().getStartParameter().getTaskRequests().toString().contains("Release")) {
    apply plugin: 'com.google.gms.google-services'
    apply plugin: 'com.google.firebase.crashlytics'
}
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

def buildTime = (new Date()).getTime()
def keyRing = new Properties()
def keyRingFile = file('../com.hzlgrn.pdxrail.keyring')
if (keyRingFile.canRead()) {
    keyRing.load(new FileInputStream(keyRingFile))
}

android {
    namespace 'com.hzlgrn.pdxrail'
    buildToolsVersion '30.0.3'
    compileSdkVersion 33

    defaultConfig {
        applicationId 'com.hzlgrn.pdxrail'
        versionCode 1
        versionName "1.2"
        minSdkVersion 23
        targetSdkVersion 33

        buildConfigField "long", "BUILD_TIME", "${buildTime}L"
        buildConfigField "String", "KEY_API_RAIL_SYSTEM", "\"${keyRing["KEY_API_RAIL_SYSTEM"]}\""
        buildConfigField "String", "URL_API_RAIL_SYSTEM", "\"${keyRing["URL_API_RAIL_SYSTEM"]}\""
        buildConfigField "String", "HOME_URL", "\"https://${keyRing["HOME_HOST"]}/\""
        buildConfigField "String", "HOME_HOST", "\"${keyRing["HOME_HOST"]}\""
        manifestPlaceholders = [
            APPLICATION_LABEL: "PDX Rail",
            KEY_GEO_ANDROID_GOOGLE  : keyRing["KEY_API_GOOGLE"],
            HOME_HOST: keyRing["HOME_HOST"],
            HOME_URL: "https://${keyRing["HOME_HOST"]}/"
        ]
        resValue "string", "asset_statements", """[{
            "relation": ["delegate_permission/common.handle_all_urls"], 
            "target": {
                "namespace": "web", 
                "site": "https://${keyRing["HOME_HOST"]}/"
            }
        }]"""
        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation":"$projectDir/schemas".toString(),
                        "room.incremental":"true",
                        "room.expandProjection":"true"]
            }
        }
    }
    buildFeatures {
        compose true
        viewBinding true
    }
    buildTypes {
        debug {
            applicationIdSuffix '.dbg'
        }
        release {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (keyRing != null) {
                signingConfigs {
                    from_key_ring {
                        keyAlias keyRing["KEYSTORE_KEY_ALIAS"]
                        keyPassword keyRing["KEYSTORE_PASSWORD"]
                        storeFile rootProject.file(keyRing["KEYSTORE_FILE"])
                        storePassword keyRing["KEYSTORE_PASSWORD"]
                    }
                }
                signingConfig signingConfigs.from_key_ring
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    composeOptions {
        kotlinCompilerExtensionVersion version_compose
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

}
dependencies {
    implementation platform('com.google.firebase:firebase-bom:28.2.1')

    implementation "androidx.appcompat:appcompat:1.6.0"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation "androidx.constraintlayout:constraintlayout-compose:1.0.1"
    implementation "androidx.core:core-ktx:1.9.0"
    implementation "androidx.recyclerview:recyclerview:1.2.1"
    implementation "com.google.android.gms:play-services-instantapps:18.0.1"
    implementation "com.google.android.gms:play-services-location:21.0.1"
    implementation "com.google.android.gms:play-services-maps:18.1.0"
    implementation "com.google.android.material:material:1.7.0"
    implementation "com.google.firebase:firebase-analytics"
    implementation "com.google.firebase:firebase-crashlytics-ktx"
    implementation "com.google.firebase:firebase-analytics-ktx"
    implementation 'com.google.maps.android:android-maps-utils:2.3.0'
    implementation "com.jakewharton.timber:timber:4.7.1"
    implementation "com.squareup.okhttp3:okhttp:4.9.1"
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$version_kotlin"

    // Compose
    implementation 'androidx.activity:activity-compose:1.6.1'
    implementation "androidx.compose.animation:animation:$version_compose"
    implementation 'androidx.compose.material:material:1.3.1'
    implementation "androidx.compose.ui:ui-tooling:$version_compose"
    // When using a MDC theme
    implementation "com.google.android.material:compose-theme-adapter:1.2.1"
    // When using a AppCompat theme
    implementation "com.google.accompanist:accompanist-appcompat-theme:0.16.0"

    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$version_compose"

    // Coroutines
    def version_coroutines = '1.5.2'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$version_coroutines"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$version_coroutines"

    // Dagger
    def version_dagger = '2.40'
    implementation "com.google.dagger:dagger:$version_dagger"
    kapt "com.google.dagger:dagger-compiler:$version_dagger"

    // Lifecycle
    def version_lifecycle = '2.2.0'
    implementation "androidx.lifecycle:lifecycle-extensions:$version_lifecycle"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.5.1"

    // Moshi
    def version_moshi = '1.12.0'
    implementation "com.squareup.moshi:moshi:$version_moshi"
    implementation "com.squareup.moshi:moshi-kotlin:$version_moshi"
    implementation 'com.squareup.retrofit2:converter-moshi:2.9.0'

    // Room
    def version_room = '2.5.0'
    implementation "androidx.room:room-runtime:$version_room"
    implementation "androidx.room:room-ktx:$version_room"
    kapt "androidx.room:room-compiler:$version_room"

    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$version_coroutines"

    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}
